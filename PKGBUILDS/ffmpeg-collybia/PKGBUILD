# Maintainer: Collybia Team <info@collybia.com>
# Website: http://collybia.com
# Maintainer: Mike Andonov <info@archphile.org>
# Website: http://archphile.org
# Based on ffmpeg-headless from AUR made by the following people
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Paul Mattal <paul@archlinux.org>

_pkgname=ffmpeg
pkgname=ffmpeg-archphile
pkgver=4.2.1
pkgrel=4
epoch=1
pkgdesc='The FFmpeg media toolkit optimized for server (headless) systems'
arch=('armv7h' 'aarch64' 'x86_64')
url='http://ffmpeg.org/'
license=('GPL3')
depends=('bzip2' 'glibc' 'gnutls' 'gsm'
         'lame' 'libmodplug'
         'libtheora' 'libxml2'
         'opus'
         'xz' 'zlib'
         'libvorbis.so'
         'rtmpdump')
makedepends=('yasm')
provides=('libavcodec.so' 'libavdevice.so' 'libavfilter.so' 'libavformat.so'
          'libavresample.so' 'libavutil.so' 'libpostproc.so' 'libswresample.so'
          'libswscale.so' 'ffmpeg')
conflicts=('ffmpeg')
source=("https://ffmpeg.org/releases/${_pkgname}-${pkgver}.tar.xz")
sha256sums=('cec7c87e9b60d174509e263ac4011b522385fd0775292e1670ecc1180c9bb6d4')

prepare() {
  cd ${_pkgname}-${pkgver}

}

build() {

	cd ${_pkgname}-${pkgver}

	[[ $CARCH == "armv7h" || $CARCH == "aarch64" ]] && CONFIG='--host-cflags="-fPIC"'

  ./configure \
    --prefix='/usr' \
    --disable-debug \
    --disable-static \
    --disable-stripping \
    --disable-vdpau \
    --disable-xlib \
    --disable-sdl2 \
    --disable-htmlpages \
    --disable-ffplay \
    --disable-libass \
    --disable-libfreetype \
    --disable-fontconfig \
    --disable-libiec61883 \
    --disable-libpulse \
    --disable-libsoxr \
    --disable-libssh \
    --disable-libvidstab \
    --disable-nvenc \
    --disable-omx \
    --disable-libxcb \
    --disable-libopenjpeg \
    --disable-libx264 \
    --disable-libx265 \
    --disable-libv4l2 \
    --disable-libopencore_amrnb \
    --disable-libopencore_amrwb \
    --disable-libdrm \
    --disable-libspeex \
    --disable-libbluray \
    --disable-libfribidi \
    --disable-libwebp \
    --disable-filters \
    --disable-parsers \
    --disable-indevs \
    --disable-outdevs \
    --disable-encoders \
    --disable-libxvid \
    --disable-libvpx \
    --disable-vaapi \
    --disable-gmp \
    --enable-decoders \
    --enable-avresample \
    --enable-gnutls \
    --enable-gpl \
    --enable-libgsm \
    --enable-libmodplug \
    --enable-libmp3lame \
    --enable-swresample \
    --enable-libopus \
    --enable-libtheora \
    --enable-libvorbis \
    --enable-libxml2 \
    --enable-shared \
    --enable-version3 \
    --enable-librtmp \
    --enable-runtime-cpudetect


  make
}

package() {
  make DESTDIR="${pkgdir}" -C ${_pkgname}-${pkgver} install install-man
}
