# Maintainer: Collybia Team <info@collybia.com>
# Website: https://collybia.com
# Maintainer: Mike Andonov <info@archphile.org>
# Website: http://archphile.org
# Based on upmpdcl from AUR made by the following people:
# Maintainer: Mansour Behabadi <mansour@oxplot.com>
# Maintainer: Tilman Vatteroth <tilman.vatteroth@udo.edu>

pkgname=upmpdcli-collybia
_pkgname=upmpdcli
pkgver=1.4.10
pkgrel=2
pkgdesc="A UPnP Media Renderer front-end for the Music Player Daemon (MPD)"
arch=('armv7h' 'aarch64')
url="http://www.lesbonscomptes.com/upmpdcli/"
license=('GPL2')
depends=('python' 'libmpdclient' 'libupnpp-collybia>=0.17.0' 'libmicrohttpd' 'jsoncpp' 'python-requests')
makedepends=('python-setuptools' 'libmpdclient' 'libupnpp-collybia>=0.17.0' 'libmicrohttpd' 'jsoncpp' 'python-requests' 'recoll' 'python-bottle' 'python-mutagen' 'aspell-en' 'id3lib' 'python-waitress' 'sqlite')
optdepends=('python2: OpenHome Radio Service' 'recoll: enable search' 'python-bottle: enable uprcl media server' 'python-mutagen: enable uprcl media server' 'mutagen: enable uprcl media server' 'aspell-en' 'id3lib' 'python-waitress: enable uprcl media server' 'sqlite')
install=upmpdcli.install
source=("http://www.lesbonscomptes.com/upmpdcli/downloads/${_pkgname}-${pkgver}.tar.gz"
	'upmpdcli.service'
	'upmpdcli.conf'
	)
sha256sums=('ecb881d7e22dcb064626ea12d1c582de73020f33bade8a3f00fd0c03cfa59da5'
            '7144c7f96942c758ffae26d64dc83bf7c2ba142c69d7f83afea5b2fe7ae886f8'
            'b7af6c8a6ea2bf0d87af218dc5b21bab2d0e5d5f7a91d6274edfa0178d597d9b')
backup=('etc/upmpdcli.conf')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  ./configure --sysconfdir=/etc --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 systemd/upmpdcli.service ${pkgdir}/usr/lib/systemd/system/upmpdcli.service
  sed '/\[Service\]/a User=upmpdcli' -i ${pkgdir}/usr/lib/systemd/system/upmpdcli.service
  sed -i '/^After/ s/$/ mpd.service/' ${pkgdir}/usr/lib/systemd/system/upmpdcli.service
  install -Dm644 $srcdir/upmpdcli.service ${pkgdir}/usr/lib/systemd/user/upmpdcli.service
  mkdir -p ${pkgdir}/etc/
  cp ../../upmpdcli.conf ${pkgdir}/etc/
  chmod 644 ${pkgdir}/etc/upmpdcli.conf
}

